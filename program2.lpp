%{
#include"lex.h"
using namespace std;
int coln = 1;
int errn = 0;
%}

%option outfile="program2_lex.cpp"
%option noyywrap
%option yylineno

SLCOMMENT   (\/\/.*[\n])
KEYWORD     "int"|"void"|"class"|"new"|"print"|"read"|"return"|"while"|"if"|"else"|"this" 
NUMBER      [0-9]+
RELOP       "=="|"!="|"<="|">="|">"|"<"
SUMOP       "+"|"-"|"||"
PRODOP      "*"|"/"|"%"|"&&"
UNAOP       "+"|"-"|"!"
OPRS        [\[\]\{\}\;\,\(\)\=\.]|"//" 
ERRTERM     [\[\]{}<>!\+\-\*\/%;,()=. \t\n]
ID          [\_A-Za-z][\_A-Za-z0-9]* 
NOTERRTERM  [^ \n\t\[\]\{\}!&|=<>\+\-\*\/\%\;\,\(\)]

%x COMMENT INNERERROR WORDERROR

%%

"/*" {
  updateAtts(yylineno, coln, yyleng, "COMMENT", "MULTILINE");
  BEGIN(COMMENT);
  return COMM; 
}

<COMMENT>[^*\n]* {
  coln += yyleng;
}

<COMMENT>"*"+[^*/\n]* {
  coln += yyleng; 
} 

<COMMENT>\n {
  coln = 1; 
}

<COMMENT>"*"+"/" {
  coln += yyleng;
  BEGIN(INITIAL); 
}

{SLCOMMENT} {
  updateAtts(yylineno, coln, yyleng, "COMMENT", yytext);
  coln = 1; 
  return COMM; 
}

{KEYWORD} {
  updateAtts(yylineno, coln, yyleng, yytext, "");
  coln+= yyleng;
  return KEYW; 
}

{RELOP} {
  updateAtts(yylineno, coln, yyleng, "R-OP", yytext);
  coln += yyleng; 
  return ROP; 
}

{SUMOP} {
  updateAtts(yylineno, coln, yyleng, "S-OP", yytext);
  coln += yyleng; 
  return SOP; 
}

{PRODOP} {
  updateAtts(yylineno, coln, yyleng, "P-OP", yytext);
  coln += yyleng; 
  return POP; 
}

{UNAOP} {
  updateAtts(yylineno, coln, yyleng, "U-OP", yytext);
  coln += yyleng; 
  return UOP; 
} 

{OPRS} { 
  updateAtts(yylineno, coln, yyleng, yytext, "");
  coln += yyleng;
  return OPS; 
}

{ID} {
  updateAtts(yylineno, coln, yyleng, "ID", yytext);
  coln+= yyleng;
  return IDEN; 
}

{NUMBER} {
  updateAtts(yylineno, coln, yyleng, "NUMBER", yytext);
  coln+= yyleng;
  return NUM; 
}

[ ] {
  updateAtts(yylineno, coln, yyleng, "SPACE", "");
  coln+= yyleng;
  return WSPC; 
}

[\n] {
  updateAtts(yylineno, coln, yyleng, "NEWLINE", "");
  coln = 1;
  return WSPC; 
}

[\t] {
  updateAtts(yylineno, coln, yyleng, "WSPACE", "");
  coln+= yyleng;
  return WSPC; 
}

. {
  errn++;
  yyless(0);
  BEGIN(WORDERROR);
  if(errn > 20) {
    return MAXERR;
  }
}

<WORDERROR>{NOTERRTERM}* {
  yymore();
  BEGIN(INNERERROR); 
} 

<WORDERROR>[ \n\t\[\]\{\}!=<>\+\-\*\/\%\;\,\(\)] {
  yyless(yyleng-1);
  updateAtts(yylineno, coln, yyleng, yytext, "WORD ERROR");
  coln += yyleng;
  BEGIN(INITIAL);
  return WRDERR; 
}

<WORDERROR><<EOF>> {
  yyless(yyleng-1);
  updateAtts(yylineno, coln, yyleng, yytext, "WORD ERROR");
  coln += yyleng;
  BEGIN(INITIAL);
  return WRDERR; 
}

<WORDERROR>[&|] {
  yymore(); 
}

<INNERERROR>"&&"|"||" {
  yyless(yyleng-2);
  updateAtts(yylineno, coln, yyleng, yytext, "WORD ERROR");
  coln += yyleng;
  BEGIN(INITIAL);
  return WRDERR; 
}

<INNERERROR>{ERRTERM} {
  yyless(yyleng-1);
  if(yyleng == 1) {
    updateAtts(yylineno, coln, yyleng, yytext, "CHAR ERROR");
  } else { 
    updateAtts(yylineno, coln, yyleng, yytext, "WORD ERROR");
  }
  coln += yyleng;
  BEGIN(INITIAL);
  return WRDERR; 
}

<INNERERROR>. {
  yymore();
  BEGIN(WORDERROR); 
}

%%
